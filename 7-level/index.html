<!-- Created with https://github.com/Naheel-Azawy/blorgify -->
<html>
    <head>
        <meta charset="utf-8" />
        <meta name="viewport" content="width=device-width, initial-scale=1.0">
        <title>Use an accelerometer to build a level</title>
        <link rel="icon" href="../icon.png">
        <link rel="stylesheet" type="text/css" href="../styles.css">
        <link rel="stylesheet" type="text/css" href="../prism.css">
        <script src="../prism.js"></script>
    </head>
    <body>
        
        <div class="content">
            <h1>Use an accelerometer to build a level</h1>

<p><img src="./doc_2020-06-23_21-42-12.mp4.gif" alt="./doc_2020-06-23_21-42-12.mp4.gif" title="./doc_2020-06-23_21-42-12.mp4.gif"/></p>
<h2>Know it</h2>
To build our bubble level we will be using an ADXL345 accelerometer. This can be used to know if a surface (say, table) is horizontal or not. Well, that one simple usage of it! ADXL345 can give us the orientation in the 3 dimensions by measuring the acceleration. This means you can use it to know the orientation of your robot or whatever huge project that you can think about. But for the sake of example, we will be building a simple bubble level here.<p><img src="./real-level.jpg" alt="./real-level.jpg" title="./real-level.jpg"/></p>
<p>An LCD screen is used here to display the readings and show a bubble to look closer to a real level (in the x-axis only..) for extra fanciness.</p>
<h2>Wire it</h2>
The LCD screen we&#39;re using here is a 16x2 character display with an I2C driver like the one <a href="https://www.learn.voltaat.com/post/lcd-i2c">HERE</a> and the ADXL345 also uses I2C interface. For this reason both, the LCD and the ADXL345 will be connected to the same pins on the Arduino with the small addition of two 10KΩ (pull-up) resistors. This is possible because I2C data line (SDA) works like bus. More than one component can use the same line. However, every component must have an address (you can see that in the code). The SCL line is just a clock to let components know when to send and receive data (synchronize).<p>Enough talking about I2C! Let&#39;s wire things up, you&#39;ll need:</p>
<ul><li>ADXL345 accelerometer</li>
<li>16x2 LCD with I2C driver</li>
<li>Jumper wires and Breadboard</li>
<li>Arduino board and USB connector</li>
</ul>
<p><img src="./level_bb_new.png" alt="./level_bb_new.png" title="./level_bb_new.png"/></p>
<p>Connections from the LCD I2C driver:</p>
<ul><li>LCD GND → Breadboard ground line</li>
<li>LCD Vcc → Arduino pin 5V</li>
<li>LCD SDA → Breadboard SDA line</li>
<li>LCD SCL → Breadboard SCL line</li>
</ul>
<p>Connections from ADXL345:</p>
<ul><li>ADXL345 GND → Breadboard ground line</li>
<li>ADXL345 Vc  → Breadboard 3.3V line</li>
<li>ADXL345 SDA → Breadboard SDA line</li>
<li>ADXL345 SCL → Breadboard SCL line</li>
</ul>
<p>Connections from the Arduino to the breadboard:</p>
<ul><li>Arduino pin GND  → Breadboard ground line</li>
<li>Arduino pin 3.3V → Breadboard 3.3V line</li>
<li>Arduino pin A4   → Breadboard SDA line</li>
<li>Arduino pin A5   → Breadboard SCL line</li>
</ul>
<p>Resistors connections:</p>
<ul><li>1st 10K resistor → Between Breadboard SDA line and 3.3V line</li>
<li>2nd 10K resistor → Between Breadboard SCL line and 3.3V line</li>
</ul>
<h2>Code it</h2>
<pre class="prettyprint"><code class="language-c">/*
Level using accelerometer

Tutorial link: <a href="https://www.learn.voltaat.com/post/use-an-accelerometer-to-build-a-level">https://www.learn.voltaat.com/post/use-an-accelerometer-to-build-a-level</a>

This is an Arduino sketch that simulates a level using an ADXL345 accelerometer and displaying it on an LCD screen

Components Needed:
1. ADXL345 ........x1
2. LCD screen .....x1
3. 10K resistor ...x2

Connections:
LCD GND          → Breadboard ground line
LCD Vcc          → Arduino pin 5V
LCD SDA          → Breadboard SDA line
LCD SCL          → Breadboard SCL line
ADXL345 GND      → Breadboard ground line
ADXL345 Vcc      → Breadboard 3.3V line
ADXL345 SDA      → Breadboard SDA line
ADXL345 SCL      → Breadboard SCL line
Arduino pin GND  → Breadboard ground line
Arduino pin 3.3V → Breadboard 3.3V line
Arduino pin A4   → Breadboard SDA line
Arduino pin A5   → Breadboard SCL line
1st 10K resistor → Between Breadboard SDA line and 3.3V line
2nd 10K resistor → Between Breadboard SCL line and 3.3V line

*/

#include &#60;Wire.h&#62;               // I2C library
#include &#60;Adafruit_Sensor.h&#62;    // Adafruit sensor library
#include &#60;Adafruit_ADXL345_U.h&#62; // ADXL345 Adafruit&#39;s library
#include &#60;LiquidCrystal_I2C.h&#62;  // LCD library

// ADXL345 instance (12345 is the I2C address)
Adafruit_ADXL345_Unified accel = Adafruit_ADXL345_Unified(12345);

// LCD instance (0x27 is the I2C address)
LiquidCrystal_I2C lcd(0x27, 16, 2);

// Commands inside void setup run once
void setup() {

  Serial.begin(9600); // Start the serial monitor
  lcd.init();         // Initialize the LCD screen
  lcd.backlight();    // Turn on backlight on the LCD

  // Initialise the sensor
  if(!accel.begin()) {
    // There was a problem detecting the ADXL345 ... check your connections
    Serial.println(&#34;Ooops, no ADXL345 detected ... Check your wiring!&#34;);
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print(&#34;Sensor error&#34;);
    while(1);
  }

  // Set the range to whatever is appropriate for your project
  // Uncomment the needed line
  accel.setRange(ADXL345_RANGE_16_G);
  // accel.setRange(ADXL345_RANGE_8_G);
  // accel.setRange(ADXL345_RANGE_4_G);
  // accel.setRange(ADXL345_RANGE_2_G);

}

// Commands inside void loop run forever
void loop() {
  
  // Get a new sensor event
  sensors_event_t event;
  accel.getEvent(&#38;event);

  // Print to serial monitor (acceleration is measured in m/s^2)
  Serial.print(&#34;X: &#34;);   Serial.print(event.acceleration.x);
  Serial.print(&#34;, Y: &#34;); Serial.print(event.acceleration.y);
  Serial.print(&#34;, Z: &#34;); Serial.print(event.acceleration.z);
  Serial.println(&#34; m/s^2 &#34;);

  // Calculate the bubble location on the screen
  // One row on the LCD is 16 characters. But we use only 15 to get
  // an odd number for the center value. The x-axis range between
  // -10 and 10. With basic math, we get the following
  int bubble = round(15 - (((event.acceleration.x + 10) / 20.0) * 15));
  if (bubble &#60; 0)  bubble = 0;
  if (bubble &#62; 14) bubble = 14;

  // Print the bubble
  lcd.setCursor(0, 0);
  for (int i = 0; i &#60; 16; ++i) {
    if (i == bubble) {
      lcd.print(&#39;o&#39;);
    } else if (i == 5 || i == 9) {
      lcd.print(&#39;|&#39;);
    } else {
      lcd.print(&#39; &#39;);
    }
  }

  // Pretty-print axes to the LCD
  char buf[17];
  sprintf(buf, &#34;X:%02d Y:%02d Z:%02d&#34;,
          (int) event.acceleration.x,
          (int) event.acceleration.y,
          (int) event.acceleration.z);
  lcd.setCursor(0, 1);
  lcd.print(buf);

  // Wait for 0.1 second
  delay(100);

}
</code>
</pre>
<h2>Show it</h2>
<img src="./20200623_214517_1.jpg" alt="./20200623_214517_1.jpg" title="./20200623_214517_1.jpg"/>
            <div style="font-size:small;text-align:right" class="meta">
                Author: Naheel Faisal Kamal <br>
                Last modified: September 2, 2020
            </div>
        </div>
    </body>
</html>
